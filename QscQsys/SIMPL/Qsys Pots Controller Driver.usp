#SYMBOL_NAME "Qsys Pots Controller Driver"

#INCLUDEPATH "..\\QscQsys\\bin\\Debug"
#USER_SIMPLSHARP_LIBRARY "QscQsys" 

#DEFINE_CONSTANT true 1
#DEFINE_CONSTANT false 0

Digital_Input Dial, Redial, Disconnect, AutoAnswerToggle, DndToggle, _skip_, KeyPad[10,10];
String_Input DialString[1000];

Digital_Output Connected, Ringing, _skip_, AutoAnswerStatus, DndStatus;
String_Output _skip_,_skip_,_skip_,_skip_,_skip_,_skip_, _skip_, _skip_,_skip_,_skip_,_skip_,  DialStringOut, CurrentlyCalling;

QsysPotsControllerSimpl pots;

String_Parameter _skip_,_skip_,_skip_,_skip_,_skip_,_skip_, _skip_, _skip_,_skip_,_skip_,_skip_,_skip_, _skip_,_skip_,_skip_,_skip_,_skip_,_skip_, ComponentName[100];
integer_parameter CoreID;

Push Dial
{
    if(len(DialString))
    {
        pots.Dial(DialString);
    }
    else
    {
        pots.DialWithoutString();
    }
}

Push Redial
{
    pots.Redial();
}

Push Disconnect
{
    pots.Disconnect();
}

Push AutoAnswerToggle
{
    pots.AutoAnswerToggle();
}

Push DndToggle
{
    pots.DndToggle();
}

Threadsafe Push KeyPad
{
    integer x;

    x = GetLastModifiedArrayIndex();

    pots.NumPad(ItoA(x - 1));
}

Callback Function NewOffHookEvent(integer value)
{
    if(value)
        Connected = true;
    else
        Connected = false;
}

Callback Function NewRingingEvent(integer value)
{
    if(value)
        Ringing = true;
    else
        Ringing = false;
}

Callback Function NewAutoAnswerEvent(integer value)
{
    if(value)
        AutoAnswerStatus = true;
    else
        AutoAnswerStatus = false;
}

Callback Function NewDndEvent(integer value)
{
    if(value)
        DndStatus = true;
    else
        DndStatus = false;
}

Callback Function NewDialStringEvent(string newDialString)
{
    DialStringOut = newDialString;
}

Callback Function NewCurrentlyCallingEvent(string newCurrentlyCalling)
{
    CurrentlyCalling = newCurrentlyCalling;
}

Function Main()
{
    RegisterDelegate(pots, onOffHookEvent, NewOffHookEvent);
    RegisterDelegate(pots, onRingingEvent, NewRingingEvent);
    RegisterDelegate(pots, onAutoAnswerEvent, NewAutoAnswerEvent);
    RegisterDelegate(pots, onDndEvent, NewDndEvent);
    RegisterDelegate(pots, onDialStringEvent, NewDialStringEvent);
    RegisterDelegate(pots, onCurrentlyCallingEvent, NewCurrentlyCallingEvent);
    pots.Initialize(CoreID, ComponentName);
}
