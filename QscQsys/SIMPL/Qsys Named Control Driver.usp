#SYMBOL_NAME "Qsys Named Control Driver"

#INCLUDEPATH "..\\QscQsys\\bin\\Debug"
#USER_SIMPLSHARP_LIBRARY "QscQsys"

Digital_Input ButtonOn, ButtonOff, ButtonMomentary, ButtonToggle, Trigger;
Analog_Input Value, Position, RampTimeMS;
String_Input StringSet[255];

Digital_Output ButtonIsOn, ButtonIsOff, _skip_, _skip_, Triggered;
Analog_Output ValueIs, PositionIs, _skip_;
String_Output StringOut;
                                   
QsysNamedControlSimpl ctrl;

String_Parameter _skip_,_skip_,_skip_,_skip_,_skip_,_skip_, _skip_, _skip_, _skip_, _skip_, Control_Name[100];
integer_parameter CoreID, NamedControlType, DefaultQsysRampTimeMS;

Push ButtonOn
{
    ctrl.SetState(1);
}

push ButtonOff
{
    ctrl.SetState(0);
}

push ButtonToggle
{
    ctrl.SetStateToggle();
}

push ButtonMomentary
{
    ctrl.SetState(1);
}
release ButtonMomentary
{
   ctrl.SetState(0);
}

push Trigger
{
    ctrl.Trigger();
}

Threadsafe change StringSet
{
   ctrl.SetString(StringSet);
}

Threadsafe Change Value
{
    integer x;
    While(x != Value)
    {
        x = Value;
        ctrl.SetValue(Value);
    }
}
Threadsafe Change Position
{
    integer x;
    While(x != Position)
    {
        x = Position;
        ctrl.SetPosition(Position);
    }
}

Change RampTimeMS
{
    ctrl.RampTimeMS(RampTimeMS);
}

Callback Function onValueIntChange(integer val, string valString)
{
    StringOut = valString;
    ValueIs = val;
}

Callback Function onValueFloatChange(signed_integer val, string valString)
{
    StringOut = valString;
    ValueIs = val;
}

Callback Function onPositionChange(integer pos)
{
    PositionIs = pos;
}

Callback Function onStateChange(integer value)
{
    if (NamedControlType = 3)
    {
        pulse(20, Triggered);
    }
    else
    {
        switch(value)
            {
                case (1):
                {
                    ButtonIsOff = OFF;
                    ButtonIsOn = ON;
                }
                case (0):
                {
                    ButtonIsOff = ON;
                    ButtonIsOn = OFF;
                }
            }
    }
}
Callback Function onStringChange(string value)
{
    StringOut = value;
}

Function Main()
{              
    RegisterDelegate(ctrl, newValueIntChange, onValueIntChange);
    RegisterDelegate(ctrl, newValueFloatChange, onValueFloatChange);
    RegisterDelegate(ctrl, newPositionChange, onPositionChange);
    RegisterDelegate(ctrl, newStateChange, onStateChange);
    RegisterDelegate(ctrl, newStringChange, onStringChange);
    ctrl.Initialize(CoreID, Control_Name, NamedControlType);                            
    ctrl.RampTimeMS(DefaultQsysRampTimeMS);
}
