#SYMBOL_NAME "Qsys Gain Component Driver"

#INCLUDEPATH "..\\QscQsys\\bin\\Debug"
#USER_SIMPLSHARP_LIBRARY "QscQsys"

Digital_Input MuteOn, MuteOff, MuteToggle, _skip_, _skip_;
Analog_Input Volume, Position, RampTimeMS;
String_Input StringVolSet[255];

Digital_Output MuteIsOn, MuteIsOff;
Analog_Output _skip_,_skip_,_skip_, VolumeIs, PositionIs;
string_output StringOut;
                                                   
QsysGainComponentSimpl gc;

String_Parameter _skip_,_skip_,_skip_,_skip_,_skip_,_skip_, _skip_, _skip_, _skip_, ComponentName[100];
integer_parameter CoreID, DefaultQsysRampTimeMS;

Push MuteOn
{
    gc.SetMute(1);
}

Push MuteOff
{
    gc.SetMute(0);
}

Push MuteToggle
{
    gc.ToggleMute();
}


Threadsafe Change Volume
{
    integer x;
    While(x != Volume)
    {
       x = Volume;
        gc.SetVolume(Volume);
    }
}

Threadsafe Change Position
{
    integer x;
    While(x != Position)
    {
        x = Position;
        gc.SetVolumePosition(Position);
    }
}

Change RampTimeMS
{
    gc.RampTimeMS(RampTimeMS);
}

Threadsafe change StringVolSet
{
    gc.SetVolumeString(StringVolSet);
}

Callback Function onVolumeChange(signed_integer val, string valString)
{
    StringOut = valString;
    VolumeIs = val;
}

Callback Function onPositionChange(integer pos)
{
    PositionIs = pos;
}

Callback Function onMuteChange(integer value)
{
    switch(value)
    {
        case (1):
        {
            MuteIsOff = OFF;
            MuteIsOn = ON;
        }
        case (0):
        {
            MuteIsOff = ON;
            MuteIsOn = OFF;
        }
    }
}
Callback Function onStringChange(string value)
{
    StringOut = value;
}

Function Main()
{
    RegisterDelegate(gc, newVolumeChange, onVolumeChange);
    RegisterDelegate(gc, newVolumePositionChange, onPositionChange);
    RegisterDelegate(gc, newMuteChange, onMuteChange);
    RegisterDelegate(gc, newStringChange, onStringChange);
    gc.Initialize(CoreID, ComponentName);                                  
    gc.RampTimeMS(DefaultQsysRampTimeMS);
}
